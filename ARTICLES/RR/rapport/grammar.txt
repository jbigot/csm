program ::= "mesh:" meshid
            "mesh entities:" listmeshent
            "computation domains:"
                      listcompdom
            "independent:"
                      listinde
            "stencil shapes:"
                      liststencil
            "data:" listdata
            "scalar" listscalar
            listloop

listmeshent ::= meshent listmeshent |  meshent
listcompdom ::= compdom listcompdom |  compdom
compdom ::= compdomid "in" listmeshent
listinde ::= inde listinde |  inde
inde ::= compdomid "and" compdomid
liststencil ::= stencil liststencil | stencil
listdata ::= data listdata |  data
data ::= dataid "," meshent
listscalar ::= scalar listscalar | scalar

listloop ::= loop listloop | loop
loop ::=  "time:" iteration
          "computations:" listcomp

iteration ::= num | criteria
criteria ::= scalar "=" criteriaid "(" listscalarread "," listdataread ")"
listscalarread ::= scalar listscalarread |  scalar
listcomp ::= comp listcomp |  comp
comp ::= dataid "[" compdomid "]=" compid "(" listscalarread "," listdataread ")"
listdataread ::= dataread listdataread |  dataread
dataread ::= dataid "[" neighborid "]" |  dataid
